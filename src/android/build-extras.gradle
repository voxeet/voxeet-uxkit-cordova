//build for voxeet's SDK
buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.+'
        classpath 'com.google.gms:google-services:3.2.0'
    }
}

repositories {
    google()
    jcenter()
    mavenLocal()
}

android {
    defaultConfig {
        // Enabling multidex support.
        multiDexEnabled true
    }
    dexOptions {
        jumboMode true
        incremental true
        javaMaxHeapSize "4g"
    }
}


// check for the environment variable to apply the push notification
// to enable push notification in builds :
// <widget ...>
//   ...
//   <preference name="VOXEET_CORDOVA_USE_PUSH" value="true" />
// </widget>
def push = false

new XmlSlurper().parse(file('../app/src/main/res/xml/config.xml')).preference.each {
    if (it.@name.text() == 'VOXEET_CORDOVA_USE_PUSH') {
        def value = it.@value.text().toLowerCase()
        push = "true".equalsIgnoreCase(value)
    }
}

dependencies {
    compile 'com.android.support:multidex:1.0.3'
    compile ('com.voxeet.sdk:toolkit:2.0.73.16') {
        transitive = true
    }

    if(push) {
        compile ('com.voxeet.sdk:toolkit-firebase:0.0.6')
    }
}

//Voxeet does need the 28 version
//in case of incompatibility, please open up an issue
//or consider upgrading if possible
ext.cdvCompileSdkVersion = 28
ext.cdvBuildToolsVersion = "28.0.3"

//use the push variable
if (push) {
  cdvPluginPostBuildExtras.add({
    apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
  })
}